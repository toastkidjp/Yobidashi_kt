plugins {
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.6.21'
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.triplet.play'
apply plugin: "com.cookpad.android.plugin.license-tools"

apply from: 'signing/config.gradle', to: android
apply from: '../jacoco.gradle'
apply from: '../detekt.gradle'

android {
    compileSdkVersion rootProject.ext.compileSdk
    buildToolsVersion buildTools

    defaultConfig {
        applicationId "jp.toastkid.yobidashi"
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk

        versionCode versionCodeValue
        versionName versionNameValue
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules-debug.pro'
            applicationIdSuffix ".d"
            manifestPlaceholders = [app_name: "Xobidashi"]
            versionNameSuffix = ".d"
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            manifestPlaceholders = [app_name: "@string/app_name"]
        }
    }
    buildFeatures {
        compose = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }

    composeOptions {
        kotlinCompilerExtensionVersion "$libVersions.compose"
    }
    lintOptions {
        checkReleaseBuilds false
    }
}

play {
    serviceAccountCredentials.set(file("signing/Google Play Android Developer-cbf2176b721a.json"))
    track = 'alpha'
}

dependencies {
    implementation project(":lib")
    implementation project(":article")
    implementation project(":search")
    implementation project(":todo")
    implementation project(':music')
    implementation project(":pdf")
    implementation project(":api")
    implementation project(":rss")
    implementation project(":image")
    implementation project(":loan")
    implementation project(":about")
    implementation project(":barcode")
    implementation project(":ui")
    implementation project(":editor")
    implementation project(":number")

    implementation "androidx.exifinterface:exifinterface:$libVersions.exifinterface"
    implementation "androidx.work:work-runtime:2.7.1"
    implementation "androidx.webkit:webkit:1.4.0"

    // Compose dependencies.
    implementation "androidx.compose.material:material:$libVersions.compose"
    implementation "androidx.compose.runtime:runtime-livedata:$libVersions.compose"
    implementation 'androidx.activity:activity-compose:1.4.0'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$libVersions.lifecycle"
    implementation "com.google.accompanist:accompanist-navigation-animation:0.23.1"
    implementation "com.google.accompanist:accompanist-flowlayout:0.24.5-alpha"
    implementation "io.coil-kt:coil-compose:$libVersions.coil_compose"
    implementation 'com.godaddy.android.colorpicker:compose-color-picker:0.4.2'

    implementation "androidx.room:room-runtime:$libVersions.room"
    kapt "androidx.room:room-compiler:$libVersions.room"

    implementation "androidx.core:core-ktx:$libVersions.ktx"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$libVersions.coroutines"
    implementation "com.jakewharton.timber:timber:$libVersions.timber"
    implementation "com.squareup.okhttp3:okhttp:$libVersions.okhttp"
    implementation 'com.squareup.moshi:moshi:1.8.0'
    implementation 'com.squareup.retrofit2:retrofit:2.6.2'
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.3"
    implementation "com.jakewharton.retrofit:retrofit2-kotlinx-serialization-converter:0.8.0"
    implementation "org.jsoup:jsoup:$libVersions.jsoup"

    testImplementation "junit:junit:$libVersions.junit"
    testImplementation "org.robolectric:robolectric:$libVersions.robolectric"
    testImplementation "io.mockk:mockk:$libVersions.mockk"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$libVersions.coroutines_test"
}

configurations.implementation {
    exclude group: 'androidx.appcompat', module: 'appcompat'
    exclude group: 'org.jetbrains.compose.runtime', module: 'runtime'
    exclude group: 'org.jetbrains.compose.foundation', module: 'foundation'
    exclude group: 'org.jetbrains.compose.material', module: 'material'
}

repositories{
    flatDir{
        dirs 'libs'
    }
}

kapt {
    javacOptions {
        option("-Xmaxerrs", 5000)
    }
}
